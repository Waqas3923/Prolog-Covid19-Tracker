%Database of facts
%a(Person_ID,START_TIME,LAT,LONG,END_TIME,PLACE_ID).
a("Zagor",    1581491705000, 436201975, 135173496, 1581498733000, "ChIJXfPcGLl_LRMRm7vjV6MGgCg").
a("Zagor",    1583516110000, 436196546, 135116121, 1583516225000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Zagor",    1583954404000, 436186150, 135124380, 1583956250000, "ChIJDzXHbrx_LRMRBg12WxxGvmw").
a("Waqas",    1582896321000, 435886750, 135154570, 1582906522000, "ChIJy9raR7x_LRMRRhJWkTxBE40").
a("Waqas",    1583480710000, 436201975, 135173496, 1583514320000, "ChIJXfPcGLl_LRMRm7vjV6MGgCg").
a("Vito",     1583936177000, 436201975, 135173496, 1583943375000, "ChIJXfPcGLl_LRMRm7vjV6MGgCg").
a("Goku",     1584466215000, 436196546, 135116121, 1584483310000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Goku",     1584089100000, 436212457, 135104647, 1584092700000, "ChIJyWoEGLt_LRMRvgzfLV2KsVQ").
a("Vegeta",   1584466215000, 436196546, 135116121, 1584483310000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Vegeta",   1584984615000, 436196546, 135116121, 1585001710000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Piccolo",  1584089100000, 436212457, 135104647, 1584092700000, "ChIJyWoEGLt_LRMRvgzfLV2KsVQ").
a("Piccolo",  1583078700000, 436074488, 134977387, 1583080240000, "ChIJcZ9iX51_LRMRbcXeW1l8lcw").
a("Gohan",    1583755200000, 436212457, 135104647, 1583762400000, "ChIJyWoEGLt_LRMRvgzfLV2KsVQ").
a("Rufy",     1582204088000, 419109666, 125312697, 1582205405000, "ChIJD1dcv9djLxMRoKHz4Aq6BoM").
a("Rufy",     1582992300000, 436074488, 134977387, 1582994130000, "ChIJcZ9iX51_LRMRbcXeW1l8lcw").
a("Zoro",     1577840400000, 445345216, 112878593, 1577851205000, "ChIJy6FHwPrTf0cRe9b4ZQFDU1Y").
a("Conte",    1583514601000, 435693012, 135092219, 1583515505000, "ChIJb8qK2PuALRMRdemoW7-hAxE").
a("Conte",    1580563708000, 435529356, 135145473, 1580565006000, "ChIJ8wwSv_GALRMRtit_eXXkC3A").
a("Scara",    1583933288000, 436034718, 135071662, 1583934606000, "ChIJy93cCYV_LRMRwOclHJ-VEg8").
a("Scara",    1582204088000, 419109999, 125312999, 1582205405000, "ChIJD1dcv9djLxMRoKHz4Aq6BoM").
a("Luca",     1582883130000, 435885750, 135154630, 1582911024000, "ChIJy9raR7x_LRMRRhJWkTxBE40").
a("Luca",     1583478910000, 435885750, 135154630, 1583522130000, "ChIJy9raR7x_LRMRRhJWkTxBE40").
a("Luca",     1577984700000, 435529356, 135145473, 1577986550000, "ChIJ8wwSv_GALRMRtit_eXXkC3A").
a("Fabio",    1583498730000, 435885750, 135154630, 1583516710000, "ChIJy9raR7x_LRMRRhJWkTxBE40").
a("Fabio",    1583954103000, 436186150, 135124380, 1583955905000, "ChIJDzXHbrx_LRMRBg12WxxGvmw").
a("Bracco",   1583485511000, 436201975, 135173496, 1583489411000, "ChIJXfPcGLl_LRMRm7vjV6MGgCg").
a("Bracco",   1583939420000, 436201975, 135173496, 1583943300000, "ChIJXfPcGLl_LRMRm7vjV6MGgCg").
a("Bracco",   1583515830000, 436196546, 135116121, 1583532310000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Luzi",     1583484620000, 435885750, 135154630, 1583495410000, "ChIJy9raR7x_LRMRRhJWkTxBE40").
a("Luzi",     1583514300000, 435693012, 135092219, 1583516155000, "ChIJb8qK2PuALRMRdemoW7-hAxE").
a("Luzi",     1583932088000, 436033718, 135070662, 1583933405000, "ChIJy93cCYV_LRMRwOclHJ-VEg8").
a("Vallese",  1583515830000, 436196546, 135116121, 1583532310000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Khaled",   1581491700000, 436201975, 135173496, 1581498000000, "ChIJXfPcGLl_LRMRm7vjV6MGgCg").
a("Khaled",   1583078700000, 436074488, 134977387, 1583080240000, "ChIJcZ9iX51_LRMRbcXeW1l8lcw").
a("Dimitri",  1583743500000, 436212457, 135104647, 1583747100000, "ChIJyWoEGLt_LRMRvgzfLV2KsVQ").
a("Dimitri",  1583414888000, 436033718, 135070662, 1583416206000, "ChIJy93cCYV_LRMRwOclHJ-VEg8").
a("Bruce",    1583414888000, 436033718, 135070662, 1583416206000, "ChIJy93cCYV_LRMRwOclHJ-VEg8").
a("Bruce",    1582992300000, 436074488, 134977387, 1582994130000, "ChIJcZ9iX51_LRMRbcXeW1l8lcw").
a("Pippo",    1584984615000, 436196546, 135116121, 1585001710000, "ChIJ90A6ebt_LRMR22wGClGgkUo").
a("Pippo",    1577840400000, 445345216, 112878593, 1577851205000, "ChIJy6FHwPrTf0cRe9b4ZQFDU1Y").



%Positive(PERSON_ID,TAMPON_DATE).
positive("Conte",   1583362800000).
positive("Khaled",  1582844400000).
positive("Luca",    1583362800000).
positive("Rufy",    1582671600000).
positive("Vallese", 1583190000000).




% Dynamic Facts
:-dynamic infected/3.
:-dynamic input_data/4.


% Calculates the probability that a person will be infected by a
% positive.
prob_calculation(X,Y,Z,0.01):-
      X>=Y-(Z/2), X<Y-1123200000    %Y-14days< X <Y-13days
    ; X>=Y+1123200000, X=<Y+(Z/2). %Y+13days< X <Y+14days
prob_calculation(X,Y,_,0.1):-
      X>=Y-1123200000, X<Y-1036800000    %Y-13days< X <Y-12days
    ; X>Y+1036800000, X=<Y+1123200000.   %Y+12days< X <Y+13days
prob_calculation(X,Y,_,0.3):-
      X>=Y-1036800000, X<Y-864000000     %Y-12giorni< X <Y-10giorni
    ; X>Y+864000000, X=<Y+1036800000.    %Y+10days< X <Y+12days
prob_calculation(X,Y,_,0.5):-
      X>=Y-864000000, X<Y-604800000      %Y-10days< X <Y-7days
    ; X>Y+604800000, X=<Y+864000000.     %Y+7giorni< X <Y+10days
prob_calculation(X,Y,_,0.7):-
      X>=Y-604800000, X<Y-289200000      %Y-7days< X <Y-3days
    ; X>Y+289200000, X=<Y+604800000.     %Y+3days< X <Y+7giorni
prob_calculation(X,Y,_,0.9):-
      X>=Y-289200000, X=<Y+289200000.   %Y-3days< X <Y+3days







%Conversion from decimal e7 degree to radians.
to_radians(X,Y):-
    Y is ((X/10000000)*pi)/180.

% Calculate the distance between two geographic coordinates using'Haversine' formula.
%Results in meters.
calculate_distance(Lat1,Lat2,Lon1,Lon2,Distance):-
     to_radians(Lat1,Latr1),
     to_radians(Lat2,Latr2),
     Delta_phi is Lat2-Lat1,
     Delta_lambda is Lon2-Lon1,
     to_radians(Delta_phi,Delta_phi_r),
     to_radians(Delta_lambda,Delta_lambda_r),
     Earth_Radius is 6371000,
     A is ((sin(Delta_phi_r*0.5)*sin(Delta_phi_r*0.5)+
           cos(Latr1)*cos(Latr2)*sin(Delta_lambda_r*0.5)
           *sin(Delta_lambda_r*0.5))),
     C is (2*atan2(sqrt(A),sqrt(1-A))),
     Distance is (Earth_Radius*C),
     Distance<5.


%Calculates id 2 people were at the same place at the same time;
%It also calculates the period.
calculate_contact_time(T1_start,T1_end,T2_start,T2_end,Period,Tstart):-
    T1_start < T2_end,
    T2_start < T1_end,
    Tstart is max(T1_start,T2_start),
    Tend is min(T1_end,T2_end),
    Period is Tend-Tstart.



% Calculate if 2 people have been in contact
is_contact(Person1,Person2,Tstart,Distance,Place_ID,Period):-
    a(Person1,T1_start,Lat1,Lon1,T1_end,Place_ID),
    a(Person2,T2_start,Lat2,Lon2,T2_end,Place_ID),
     Person1 \= Person2,
    \+positive(Person2,_),
      calculate_distance(Lat1,Lat2,Lon1,Lon2,Distance),
    calculate_contact_time(T1_start,T1_end,T2_start,T2_end,Period,Tstart).



% Checks if the infected is already present in the dynamic database of
% infected people.
insert_infected(Person,Infection_Date,Probability):-
    \+infected(Person,Infection_Date,Probability),
    assert(infected(Person,Infection_Date,Probability)).

%  primo caso del predicato contagio


%Check if Person1 infects the person2
is_infected(Person1,(Person2,Contact_Date,Place_ID,Person1,Probability)):-
    positive(Person1,Tampon_Date),
    is_contact(Person1,Person2,Contact_Date,Distance,Place_ID,Period),
    input_data(Infection_Period,_,Min_Exposure_Time,Min_Distance),
    Date is Tampon_Date-(Infection_Period/2),
    Contact_Date >= Date,
    Distance < Min_Exposure_Time,
    Period > Min_Distance,
    prob_calculation(Contact_Date,Tampon_Date,Infection_Period,Probability),
    insert_infected(Person2,Contact_Date,Probability).

is_infected(Person1,(Person2,Contact_Date,Place_ID,Person1,New_Probability)):-
    infected(Person1,Infection_Date,Prev_Probability),
    is_contact(Person1,Person2,Contact_Date,Distance,Place_ID,Period),
    input_data(Periodo_Contagio,Infection_Period_Start,Min_Exposure_Time,Min_Distance),
    Date is Infection_Date+Infection_Period_Start,
    Date =< Contact_Date,
    Data2 is Date+(Periodo_Contagio/2),
    Distance < Min_Distance,
    Period > Min_Exposure_Time,
    prob_calculation(Contact_Date,Data2,Periodo_Contagio,Probability),
    New_Probability is Prev_Probability*Probability,
    insert_infected(Person2,Contact_Date,New_Probability).




% Depth Search Algorithm:
% Takes Person1, checks if Person2 is been infected from Person1::
%  - If Yes then inserts Person2 to the infected list and repeats the;
%  algorithm for Person2
%   -If No discards Person2 and continues the algorithm for Person 1.
are_infected_list(Person1,[(Person2,Tstart,Place_ID,Person1,Probability)|Others]):-
        is_infected(Person1,(Person2,Tstart,Place_ID,Person1,Probability)),
                are_infected_list(Person2,Others).


%goal::: when there is no infected.
are_infected_list(Person1,[]):-
    \+is_infected(Person1,_),!.


% Takes as input positive people list and for each positive trace its
% potential infected contacts.
find_all_infected([H|T],[List2|T1]):-
    findall(List,(are_infected_list(H,List)),List2),
    find_all_infected(T,T1).

find_all_infected([],[]):-!.



% Main Programm::
main:-
    retractall(infected(_,_,_)),
    retractall(input_data(_,_,_,_)),

    write("Insert the period in which a person can be contagious (in milliseconds):  "),nl,
    read(Contagious_Period),
    write("Insert incubation period (in milliseconds):  "),nl,
    read(Contagious_Period_Start),
    write("Insert minimum exposure time (in millisecondi):  "),nl,
    read(Min_Time_Exposure),
    write("Insert maximum distance(in metri):  "),nl,
    read(Dm),

    assert(input_data(Contagious_Period,Contagious_Period_Start,Min_Time_Exposure,Dm)),
    findall(Person1,positive(Person1,_),Positive_List),
    find_all_infected(Positive_List,All_Infected_List),
    flatten(All_Infected_List,Flatten_Infected_List),
    remove_duplicates(Flatten_Infected_List,Infected_List),

    nl, write("Person"),write("    "),write("Date"),
    write("                  "),
    write("Pace ID"), write("                "),
    write("Infected By"),  write("                "),
    write("Infection Probability"),nl,nl,

    print_all(Infected_List),
    retractall(infected(_,_,_)).

%Print Predicate
print_all([(Person2,Date,PlaceID,Person1,Probability)|T]):-
    write(Person2),write("      "),write(Date),write("     "), write(PlaceID), write("     "),
    write(Person1),write("                "), write(Probability),nl,

    print_all(T).
print_all([]):- !.



%Removes duplicates in a list
remove_duplicates([H|T],List):-
    member(H,T),
    remove_duplicates(T,List).
remove_duplicates([H|T],[H|T1]):-
    \+member(H,T),
    remove_duplicates(T,T1).
remove_duplicates([],[]):-!.
